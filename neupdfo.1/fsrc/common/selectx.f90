module selectx_mod

implicit none
private
public :: savefilt, selectx


contains


subroutine savefilt(constr, cstrv, ctol, f, x, nfilt, cfilt, confilt, ffilt, xfilt)
! This subroutine saves X, F, CONSTR, and CSTRV in XFILT, FFILT, CONFILT, and CFILT, unless a vector
! in XFILT(:, 1:NFILT) is better than X. If X is better than some vectors in XFILT(:, 1:NFILT), then
! these vectors will be removed. If X is not better than any of XFILT(:, 1:NFILT) but NFILT=NFILTMAX,
! then we remove XFILT(:,1), which is the oldest vector in XFILT(:, 1:NFILT).
!
! N.B.
! 1. Only XFILT(:, 1:NFILT) and FFILT(:, 1:NFILT) etc contains valid information, while
! XFILT(:, NFILT+1:NFILTMAX) and FFILT(:, NFILT+1:NFILTMAX) etc are not initialized yet.
! 2. We decide whether an X is better than another by the ISBETTER function.

use consts_mod, only : RP, IK, DEBUGGING
use debug_mod, only : errstop, verisize
use memory_mod, only : safealloc

implicit none

! Inputs
real(RP), intent(in) :: constr(:)  ! M
real(RP), intent(in) :: cstrv
real(RP), intent(in) :: ctol
real(RP), intent(in) :: f
real(RP), intent(in) :: x(:)  ! N

! In-outputs
integer(IK), intent(inout) :: nfilt
real(RP), intent(inout) :: cfilt(:)  ! NFILTMAX
real(RP), intent(inout) :: confilt(:, :)  ! (M, NFILTMAX)
real(RP), intent(inout) :: ffilt(:)  ! NFILTMAX
real(RP), intent(inout) :: xfilt(:, :) ! (N, NFILTMAX)

! Local variables
integer(IK) :: m
integer(IK) :: n
integer(IK) :: nfiltmax
integer(IK) :: i
integer(IK), allocatable :: index_to_keep(:)
logical :: better(nfilt)
logical :: keep(nfilt)
character(len=*), parameter :: srname = 'SAVEFILT'

m = size(constr)
n = size(x)
nfiltmax = size(ffilt)

if (DEBUGGING) then
    if (n == 0) then
        call errstop(srname, 'SIZE(X) == 0')
    end if
    if (nfiltmax == 0) then
        call errstop(srname, 'SIZE(FFILT) == 0')
    end if
    call verisize(xfilt, n, nfiltmax)
    call verisize(confilt, m, nfiltmax)
    call verisize(cfilt, nfiltmax)
end if

! Return immediately if any column of XFILT is better than X.
! BETTER is defined by the array constructor with an implicit do loop.
better = [(isbetter([ffilt(i), cfilt(i)], [f, cstrv], ctol), i=1, nfilt)]
if (any(better)) then
    return
end if

! Decide which columns of XFILT to keep. We use again the array constructor with an implicit do loop.
keep = [(.not. isbetter([f, cstrv], [ffilt(i), cfilt(i)], ctol), i=1, nfilt)]
! If X is not better than any column of XFILT, then we remove the first (oldest) column of XFILT.
if (count(keep) == nfiltmax) then
    keep(1) = .false.
end if

!--------------------------------------------------!
!----The SAFEALLOC line is removable in F2003.-----!
call safealloc(index_to_keep, nfilt)
!--------------------------------------------------!
index_to_keep = pack([(i, i=1, nfilt)], mask=keep)
nfilt = count(keep) + 1
xfilt(:, 1:nfilt - 1) = xfilt(:, index_to_keep)
ffilt(1:nfilt - 1) = ffilt(index_to_keep)
confilt(:, 1:nfilt - 1) = confilt(:, index_to_keep)
cfilt(1:nfilt - 1) = cfilt(index_to_keep)
! F2003 automatically deallocate local ALLOCATABLE variables at exit, yet
! we prefer to deallocate them immediately when they finish their jobs.
deallocate (index_to_keep)
xfilt(:, nfilt) = x
ffilt(nfilt) = f
confilt(:, nfilt) = constr
cfilt(nfilt) = cstrv

end subroutine savefilt


function selectx(fhist, chist, cpen, ctol) result(kopt)
! This subroutine selects X according to the FHIST and CHIST, which represents (a piece of) history
! of F and CSTRV. Normally, FHIST and CHIST are not the full history but only a filter, e.g., FFILT
! and CFILT generated by SAVEFILT. However, we name them as FHIST and CHIST because the [F, CSTRV]
! in a filter should not dominate each other, but this subroutine does NOT assume such a structure.

use consts_mod, only : IK, RP, HUGENUM, HUGEFUN, HUGECON, ZERO, TWO, DEBUGGING
use debug_mod, only : errstop, verisize

implicit none

! Inputs
real(RP), intent(in) :: cpen
real(RP), intent(in) :: chist(:)
real(RP), intent(in) :: ctol
real(RP), intent(in) :: fhist(:)

! Output
integer(IK) :: kopt

! Local variables
real(RP) :: cmin
real(RP) :: cref
real(RP) :: chist_shifted(size(fhist))
real(RP) :: fref
real(RP) :: phi(size(fhist))
real(RP) :: phimin
character(len=*), parameter :: srname = 'SELECTX'


! Get the verify the sizes.
if (DEBUGGING) then
    if (size(fhist) < 1) then
        call errstop(srname, 'SIZE(FHIST) < 1')
    end if
    call verisize(chist, size(fhist))
end if

! We select X among the points with F < FREF and CSTRV < CREF.
! Do NOT use F <= FREF, because F = FREF (HUGEFUN or HUGENUM) may mean F = INF in practice.
if (any(fhist < HUGEFUN .and. chist < HUGECON)) then
    fref = HUGEFUN
    cref = HUGECON
elseif (any(fhist < HUGENUM .and. chist < HUGECON)) then
    fref = HUGENUM
    cref = HUGECON
elseif (any(fhist < HUGEFUN .and. chist < HUGENUM)) then
    fref = HUGEFUN
    cref = HUGENUM
else
    fref = HUGENUM
    cref = HUGENUM
end if

if (.not. any(fhist < fref .and. chist < cref)) then
    kopt = size(fhist)
else
    ! Shift the constraint violations by CTOL, so that CSTRV <= CTOL is regarded as no violation.
    chist_shifted = max(chist - ctol, ZERO)
    ! CMIN is the minimal shifted constraint violation attained in the history.
    cmin = minval(chist_shifted, mask=(fhist < fref))
    ! We select X among the points whose shifted constraint violations are at most CREF.
    cref = TWO * cmin  ! CREF = ZERO if CMIN = ZERO; asking for CSTRV_SHIFTED < CREF is unreasonable.
    ! We use the following PHI as our merit function to select X.
    phi = fhist + cpen * chist_shifted
    ! We select X to minimize PHI subject to F < FREF and CSTRV_SHIFTED <= CREF. In case there are
    ! multiple minimizers, we take the one with the least CSTRV; if there are still more than one
    ! choices, we take the one with the least F; if the last comparison still leads to more than
    ! one possibilities, then they are all equally good, and we choose the first one of them.
    ! N.B.: In finite-precision arithmetic, PHI_1 = PHI_2 and CSTRV_SHIFTED_1 = CSTRV_SHIFTED_2 do
    ! not ensure that F_1 = F_2!!!
    phimin = minval(phi, mask=(fhist < fref .and. chist_shifted <= cref))
    cref = minval(chist, mask=(fhist < fref .and. phi <= phimin))
    kopt = int(minloc(fhist, mask=(chist <= cref), dim=1), kind(kopt))
end if

end function selectx


function isbetter(fc1, fc2, ctol) result(is_better)
! This function compares whether FC1 = (F1, CSTRV1) is (strictly) better than FC2 = (F2, CSTRV2),
! meaning that (F1 < F2 and CSTRV1 <= CSTRV2) or (F1 <= F2 and CSTRV1 < CSTRV2).
! It takes care of the cases where some of these values are NaN or Inf.
! At return, BETTER = TRUE if and only if (F1, CSTRV1) is better than (F2, CSTRV2).
! Here, CSTRV means constraint violation, which is a nonnegative number.

! Generic modules
use consts_mod, only : RP, TEN, HUGENUM, DEBUGGING
use infnan_mod, only : is_nan
use debug_mod, only : errstop
implicit none

! Inputs
real(RP), intent(in) :: fc1(:)
real(RP), intent(in) :: fc2(:)
real(RP), intent(in) :: ctol

! Output
real(RP) :: cref
logical :: is_better

! Local variables
character(len=*), parameter :: srname = 'ISBETTER'


! Verify the sizes
if (DEBUGGING .and. (size(fc1) /= 2 .or. size(fc2) /= 2)) then
    call errstop(srname, 'SIZE(FC1) or SIZE(FC2) is not 2')
end if

is_better = .false.
is_better = is_better .or. (.not. any(is_nan(fc1)) .and. any(is_nan(fc2)))
is_better = is_better .or. (fc1(1) < fc2(1) .and. fc1(2) <= fc2(2))
is_better = is_better .or. (fc1(1) <= fc2(1) .and. fc1(2) < fc2(2))
cref = TEN * max(ctol, epsilon(ctol))
is_better = is_better .or. (fc1(1) < HUGENUM .and. fc1(2) <= ctol .and. ((fc2(2) > cref) .or. is_nan(fc2(2))))

end function isbetter


end module selectx_mod
